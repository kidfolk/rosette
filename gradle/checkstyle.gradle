import org.gradle.api.internal.project.IsolatedAntBuilder


configurations {
    codequality
}

repositories {
    mavenCentral()
}

dependencies {
    codequality 'com.puppycrawl.tools:checkstyle:5.6'
}

task checkstyle(type: AndroidCheckstyleTask) {
    classpath = configurations.codequality
    ignoreFailures = true
    outputFile = file("$buildDir/reports/checkstyle/checkstyle.xml")
}

task checkstyleReport << {
    description = 'Applies XML stylesheet to generated checkstyle*.xml files.'
    if (file("$buildDir/reports/checkstyle/checkstyle.xml").exists()) {
        ant.xslt(in: "$buildDir/reports/checkstyle/checkstyle.xml",
                style:"$rootDir/config/checkstyle/checkstyle-noframes-sorted.xsl",
                out:"$buildDir/reports/checkstyle/checkstyle.html"
        )
    }
}

gradle.taskGraph.afterTask {Task task, TaskState state ->
    if(!checkFailOnError || state.failure) {
        if (task.name in ['checkstyle']) {
            checkstyleReport.execute()
        }
    }
}

check.dependsOn(checkstyle)

// https://github.com/BeneStem/WicketCheesr/blob/daa74f0b7ca3ee0f47e1ab902a3c756553efa156/build.gradle
// https://github.com/gradleware/oreilly-gradle-book-examples/blob/master/ant-customtaskuse/build.gradle
// http://gradle.1045684.n5.nabble.com/Trying-to-use-Checkstyle-getting-TreeWalker-error-td1435438.html


class AndroidCheckstyleTask extends DefaultTask {

    @InputFile @Optional File configFile = new File("$project.rootDir/config/checkstyle/checkstyle.xml")
    @OutputFile @Optional File outputFile = new File("$project.buildDir/reports/checkstyle/checkstyle.xml")
    @InputFiles FileCollection classpath
    Boolean ignoreFailures = false

    Project gproj = project

    @TaskAction
    def runCheckstyle() {
        outputFile.parentFile.mkdirs()
        def antBuilder = services.get(IsolatedAntBuilder)
        antBuilder.withClasspath(classpath).execute {
            ant.taskdef(name: 'checkstyle', classname: 'com.puppycrawl.tools.checkstyle.CheckStyleTask')
            // see also, maxWarnings and failureProperty arguments
            ant.checkstyle(config: configFile, failOnViolation: !ignoreFailures) {
                fileset(dir: gproj.projectDir.getPath()) {
                    gproj.android.sourceSets.each { sourceSet ->
                        sourceSet.java.each { file ->
                            include(name: gproj.relativePath(file))
                        }
                    }
                }

                // uncomment to include console output
                //formatter(type: 'plain', useFile: false)
                formatter(type: 'xml', toFile: outputFile)
            }
        }
    }
}
